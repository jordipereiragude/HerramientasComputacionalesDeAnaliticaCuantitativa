{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "using JuMP\n",
    "#using GLPKMathProgInterface\n",
    "using CPLEX"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Absolute MMR Assignment Problem\n",
    "\n",
    "Basado en Jordi Pereira, Igor Averbakh, Exact and heuristic algorithms for the interval data robust assignment problem, In Computers & Operations Research, Volume 38, Issue 8, 2011, Pages 1153-1163, ISSN 0305-0548, https://doi.org/10.1016/j.cor.2010.11.009.\n",
    "\n",
    "Utilizaremos el siguiente modelo\n",
    "\n",
    "$\\min \\sum_{(u,v)\\in E} c_{uv}^+ x_{uv} - \\theta$\n",
    "\n",
    "\n",
    "s.t.\n",
    "\n",
    "$\\theta \\leq \\sum_{(u,v)\\in E} c_{uv}^- y_{uv} + \\sum_{(u,v)\\in E} y_{uv} (c_{uv}^+ - c_{uv}^-) x_{uv}\\qquad \\forall Y\\in A$\n",
    "\n",
    "$\\sum_{u\\in U} x_{uv} = 1, \\qquad v\\in V$;\n",
    "\n",
    "$\\sum_{v\\in V} x_{uv} = 1, \\qquad u\\in U$;\n",
    "\n",
    "$x_{uv}\\in \\{0,1\\}, \\qquad u\\in U, v\\in V$.\n",
    "\n",
    "Nótese que la restricción que calcula $\\theta$ hace uso de diversas propiedades del problema (sabemos que el escenario que maximiza la diferencia entre dos soluciones es un escenario extremo de características concretas y que una de esas soluciones define la peor alternativa y por tanto el worst-case regret de una solución $X$.\n",
    "\n",
    "En realidad es más conveniente implementar como:\n",
    "\n",
    "$\\min \\theta$\n",
    "\n",
    "\n",
    "s.t.\n",
    "\n",
    "$\\theta \\geq \\sum_{(u,v)\\in E} c_{uv}^s x_{uv} - c_{uv}^s x_{uv}^{\\ast}\\forall s\\in S$\n",
    "\n",
    "y específicamente definir $s$ para una solución $x$ como su caso extremo. Esto es, $c_{uv}^s= c_{uv}^+$ si $x_{uv}=1$ y $c_{uv}^s= c_{uv}^-$ en el resto de los casos."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function doInstanciaAleatoria(n::Int64)\n",
    "    cplus = rand(1:1000000, n*n)\n",
    "    cminus=map(x -> rand(1:x), cplus)\n",
    "    return reshape(cminus,n,n),reshape(cplus,n,n)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "srand(0)\n",
    "cminus,cplus=doInstanciaAleatoria(10)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function solveAP(c,n::Int64,solver)\n",
    "    submodel = Model(solver = solver)\n",
    "    @variable(submodel, x[1:n,1:n],Bin)\n",
    "    @objective(submodel,Min, sum(c[i,j]*x[i,j] for i in 1:n, j in 1:n) )\n",
    "    \n",
    "    @constraint(submodel, origin[i = 1:n], sum(x[i,1:n]) == 1)\n",
    "    @constraint(submodel, destination[j = 1:n], sum(x[1:n,j]) == 1)\n",
    "    status = solve(submodel)\n",
    "    #print(\"solucion: \",status,\"\\n\\n\\n\")\n",
    "    valor=getobjectivevalue(submodel)\n",
    "    return valor\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function solveRAP(cminus, cplus, n::Int64, solver=CplexSolver())\n",
    "    m = Model(solver = solver)\n",
    "    \n",
    "    @variable(m, x[1:n,1:n], Bin)\n",
    "    @variable(m,θ)\n",
    "\n",
    "    @objective(m, Min, θ )\n",
    "    \n",
    "    @constraint(m, origin[i = 1:n], sum(x[i,1:n]) == 1)\n",
    "    @constraint(m, destination[j = 1:n], sum(x[1:n,j]) == 1)\n",
    "    \n",
    "    cMidPoint=zeros(Float64,n,n)\n",
    "    for i in 1:n\n",
    "        for j in 1:n\n",
    "            cMidPoint[i,j]=cminus[i,j]+cplus[i,j]\n",
    "        end\n",
    "    end\n",
    "    optimalCost = solveAP(cMidPoint,n,CplexSolver(CPX_PARAM_SCRIND=0))\n",
    "    rhs = - optimalCost \n",
    "    for i in 1:n\n",
    "        for j in 1:n\n",
    "            rhs += cMidPoint[i,j]*x[i,j]\n",
    "        end\n",
    "    end\n",
    "    @constraint(m,θ >=rhs)        \n",
    "    #print(m)\n",
    "\n",
    "    tt = 0.0\n",
    "    called = 0.0\n",
    "    separationtime=0.0\n",
    "    separated=0\n",
    "    \n",
    "    \n",
    "    function lazyGenerator(m)\n",
    "        #print(\"\\n\\n entro en generador de cortes\")\n",
    "        EPSILON=0.00001\n",
    "        tt = time()\n",
    "        called += 1\n",
    "        x_val = getvalue(x)\n",
    "                \n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                if x_val[i,j]>EPSILON && x_val[i,j]<(1.0-EPSILON)\n",
    "                    return\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        # tenemos una solucion entera, buscamos los costos\n",
    "        cost=deepcopy(cminus)\n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                if x_val[i,j]>EPSILON\n",
    "                    cost[i,j]=cplus[i,j]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        optimalCost = solveAP(cost,n,CplexSolver(CPX_PARAM_SCRIND=0))\n",
    "        #println(\"\\t\\t\",optimalCost,\"\\n\")\n",
    "       \n",
    "        rhs = - optimalCost \n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                rhs += cost[i,j]*x[i,j]\n",
    "            end\n",
    "        end\n",
    "        #println(rhs)\n",
    "        #print(\"*\")\n",
    "        @lazyconstraint(m, θ >= rhs )\n",
    "        separated += 1\n",
    "        separationtime += time()-tt\n",
    "    end\n",
    "    addlazycallback(m, lazyGenerator) #es necesario un corte lazy ya que le indica al programa que no la he incluido\n",
    "\n",
    "    status = solve(m)\n",
    "    println(status)\n",
    "    println(\"maxRegret: \",getobjectivevalue(m))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "solveRAP(cminus,cplus,10)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## Compliquemos ligeramente el ejemplo anterior\n",
    "\n",
    "Tenemos dos puntos que podríamos mejorar:\n",
    "\n",
    "(1) No estamos generando cortes para las soluciones fraccionales\n",
    "\n",
    "(2) Vamos a usar la heuristic callback para generar posibles soluciones. Debido a la particularidad de la lazy cut, ésta será la encargada de insertar los cortes\n",
    "\n",
    "La forma de implementarla va a ser un tanto \"rara\" y se debe a particularidades de este problema. \n",
    "\n",
    "Adicionalmente añadimos una \"information callback\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function solveRAPwithHeuristic(cminus, cplus, n::Int64, solver=CplexSolver(CPX_PARAM_SCRIND=0))\n",
    "    m = Model(solver = solver)\n",
    "    \n",
    "    @variable(m, x[1:n,1:n], Bin)\n",
    "    @variable(m,θ)\n",
    "\n",
    "    @objective(m, Min, θ )\n",
    "    \n",
    "    @constraint(m, origin[i = 1:n], sum(x[i,1:n]) == 1)\n",
    "    @constraint(m, destination[j = 1:n], sum(x[1:n,j]) == 1)\n",
    "    \n",
    "    cMidPoint=zeros(Float64,n,n)\n",
    "    for i in 1:n\n",
    "        for j in 1:n\n",
    "            cMidPoint[i,j]=cminus[i,j]+cplus[i,j]\n",
    "        end\n",
    "    end\n",
    "    optimalCost = solveAP(cMidPoint,n,CplexSolver(CPX_PARAM_SCRIND=0))\n",
    "    rhs = - optimalCost \n",
    "    for i in 1:n\n",
    "        for j in 1:n\n",
    "            rhs += cMidPoint[i,j]*x[i,j]\n",
    "        end\n",
    "    end\n",
    "    @constraint(m,θ >=rhs)        \n",
    "    #print(m)\n",
    "\n",
    "    tt = 0.0\n",
    "    called = 0.0\n",
    "    separationtime=0.0\n",
    "    separated=0\n",
    "    pending = JuMP.GenericAffExpr[]\n",
    "    function heuristic(m)\n",
    "        EPSILON=0.00001\n",
    "        x_val = getvalue(x)\n",
    "        xTest=zeros(Int64,n)\n",
    "        yUsed=zeros(Int64,n)\n",
    "        bestVal=0\n",
    "        for i in 1:n\n",
    "            val=(-1)\n",
    "            bestVal=0\n",
    "            for j in 1:n\n",
    "                if yUsed[j]==0 && val<x_val[i,j]\n",
    "                    val=x_val[i,j]\n",
    "                    bestVal=j\n",
    "                end\n",
    "            end\n",
    "            xTest[i]=bestVal\n",
    "            yUsed[bestVal]=1\n",
    "        end\n",
    "        \n",
    "        cost=deepcopy(cminus)        \n",
    "        for i in 1:n\n",
    "            x_val[i,:]=0\n",
    "            x_val[i,xTest[i]]=1\n",
    "            for j in 1:n\n",
    "                if x_val[i,j]>EPSILON\n",
    "                    cost[i,j]=cplus[i,j]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        optimalCost = solveAP(cost,n,CplexSolver(CPX_PARAM_SCRIND=0))\n",
    "        evaluacionCost = - optimalCost\n",
    "        rhs = - optimalCost \n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                rhs += cost[i,j]*x[i,j]\n",
    "                if x_val[i,j]>EPSILON\n",
    "                    evaluacionCost += cost[i,j]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        #println(\"\\nIn: \",MathProgBase.cbgetobj(m),\" vs \",evaluacionCost)\n",
    "        if(MathProgBase.cbgetobj(m)>evaluacionCost)\n",
    "            push!(pending,rhs)\n",
    "            setsolutionvalue(m,θ,evaluacionCost)\n",
    "            for i in 1:n\n",
    "                for j in 1:n\n",
    "                    if x_val[i,j]>EPSILON\n",
    "                        setsolutionvalue(m,x[i,j],1)\n",
    "                    else\n",
    "                        setsolutionvalue(m,x[i,j],0)\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "            addsolution(m)\n",
    "        end\n",
    "    end\n",
    "    function lazyGenerator(m)\n",
    "        #print(\"\\n\\n entro en generador de cortes\")\n",
    "        EPSILON=0.00001\n",
    "        tt = time()\n",
    "        called += 1\n",
    "        x_val = getvalue(x)\n",
    "                \n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                if x_val[i,j]>EPSILON && x_val[i,j]<(1.0-EPSILON)\n",
    "                    return\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        # tenemos una solucion entera, buscamos los costos\n",
    "        cost=deepcopy(cminus)\n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                if x_val[i,j]>EPSILON\n",
    "                    cost[i,j]=cplus[i,j]\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "        optimalCost = solveAP(cost,n,CplexSolver(CPX_PARAM_SCRIND=0))\n",
    "        #println(\"\\t\\t\",optimalCost,\"\\n\")\n",
    "       \n",
    "        rhs = - optimalCost \n",
    "        for i in 1:n\n",
    "            for j in 1:n\n",
    "                rhs += cost[i,j]*x[i,j]\n",
    "            end\n",
    "        end\n",
    "        #println(rhs)\n",
    "        @lazyconstraint(m, θ >= rhs )\n",
    "        separated += 1\n",
    "        separationtime += time()-tt\n",
    "    end\n",
    "    function cutGenerator(m)\n",
    "        while !isempty(pending)\n",
    "            rhs = pop!(pending)\n",
    "            @usercut(m,θ>=rhs)\n",
    "        end\n",
    "    end\n",
    "    function infocallback(m)\n",
    "        println(\"exploredNodes: \",MathProgBase.cbgetexplorednodes(m),\" obj: \",MathProgBase.cbgetobj(m), \" bestBound: \",MathProgBase.cbgetbestbound(m))        \n",
    "    end\n",
    "    #:MIPNode if at node in branch-and-cut tree, \n",
    "    #:MIPSol at an integer-feasible solution,\n",
    "    #:Intermediate otherwise.\n",
    "    addinfocallback(m, infocallback, when = :Intermediate)\n",
    "    addlazycallback(m, lazyGenerator) #es necesario un corte lazy ya que le indica al programa que no la he incluido\n",
    "    addcutcallback(m,cutGenerator)\n",
    "    addheuristiccallback(m,heuristic)\n",
    "    status = solve(m)\n",
    "    println(status)\n",
    "    println(\"maxRegret: \",getobjectivevalue(m))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "solveRAPwithHeuristic(cminus,cplus,10)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.5.2",
   "language": "julia",
   "name": "julia-0.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.5.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
